
void pnt1_vp(
	float4 position				: POSITION,
	float3 normal				: NORMAL,
	float2 uv					: TEXCOORD0,

	out float4 oPosition		: POSITION,
	out float2 oUv				: TEXCOORD0,
	out float4 oObjectPosition	: TEXCOORD1,
	out float4 oNormal			: TEXCOORD2,

	uniform float4x4 world,
	uniform float4x4 worldViewProj)
{
	oPosition = mul(worldViewProj, position);
	oObjectPosition = mul(world, position);
	oNormal = mul(world, float4(normal, 0));
	oUv = uv;
}


sampler DiffuseMap : register(s0);

float4 ball_fp(
	float2 uv				: TEXCOORD0,
	float4 objectPosition	: TEXCOORD1,
	float4 normal			: TEXCOORD2,

	uniform float3 cameraPosition,
	uniform float3 lightPosition,
	uniform float3 lightDirection,
	uniform float4 lightDiffuse,
	uniform float4 lightSpecular,
	uniform float4 spotLightParams,
	uniform float4 specularColor,
	uniform float shininess)
	: COLOR
{
	float4 Kd = tex2D(DiffuseMap, float2(uv.x * 2, uv.y));

	float3 N = normalize(normal.xyz);
	float3 V = normalize(cameraPosition - objectPosition.xyz);
	float3 L = normalize(lightPosition - objectPosition.xyz);

	float cosDirection = dot(L, -lightDirection);
	float spotEffect = smoothstep(spotLightParams.y, spotLightParams.x, cosDirection);

	float diffuseLight = saturate(dot(N, L));
	float4 diffuseResult = lightDiffuse * diffuseLight * Kd * spotEffect;

	float3 H = normalize(L + V);
	float specularLight = pow(max(dot(N, H), 0), shininess);
	float specularResult = lightSpecular * specularLight * specularColor * spotEffect;

	float4 result = diffuseResult + specularResult;

	return result;
}

float4 ambient_fp(
	float2 uv				: TEXCOORD0,

	uniform float4 ambientColor)
	: COLOR
{
	return ambientColor * tex2D(DiffuseMap, float2(uv.x * 2, uv.y));
}
